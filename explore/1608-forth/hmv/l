\ install library packages in flash
\ needs h
\ includes d

( cornerstone ) <<<hal-hmv>>>
cr
compiletoflash

( code-start, see code-size below ) here

include ../flib/fsmc/tft-r61505u.fs
include ../flib/spi/sdcard.fs
include ../flib/mecrisp/graphics.fs
include ../flib/mecrisp/multi.fs
include ../flib/mecrisp/sine.fs

: index>sine ( u -- u )
  0 swap  \ [0..4095] as fixed-point
  2048,0 f/ pi f*  \ [0..2pi) as fixed-point
  widesine 1500,0 f* nip 2048 + ;

\ located above the 20K RAM area used by Mecrisp's std build for STM32F103's
$20006000 constant wavetable  \ 4096 2-byte ints in range 0..4095

: fill-sinewave ( -- )
  $1000 0 do i index>sine i 2* wavetable + h! loop ;

: sinewave
  dac-init  fill-sinewave
  begin
    $1000 0 do  i 2* wavetable + h@ dup 2dac!  2 +loop
  key? until
;

: dac1-awg ( u -- )  \ generate on DAC1 via DMA with given timer period
  6 timer-init  dac-init  wavetable 8192 dac1-dma  fill-sinewave ;

\ -----------------------------------------------------------------------------
\ oscilloscope experiment

\ located above the 20K RAM area used by Mecrisp's std build for STM32F103's
$20008000 602 2constant adc-buffer  \ 301 2-byte ints in range 0..4095

: pp ( x y )  10 + swap 20 + swap putpixel ;

: border
  $0888 tft-fg !
  202 -1 do  i -1 pp  i 301 pp  loop
  302 -1 do  -1 i pp  201 i pp  loop
  $FFFF tft-fg ! ;

: grid
  $0888 tft-fg !
  225 0 do
    305   0 do  j i pp  5 +loop          \ horizontal grid lines
    i 148 pp  i 152 pp                   \ major tick marks on Y axis
  25 +loop
  205 0 do  i 149 pp  i 151 pp  5 +loop  \ minor tick marks on Y axis
  325 0 do
    205  0 do  i j pp  5 +loop           \ vertical grid lines
    98 i pp 102 i pp                     \ major tick marks on X axis
  25 +loop
  305 0 do  99 i pp 101 i pp  5 +loop    \ minor tick marks on X axis
  $FFFF tft-fg ! ;

602 buffer: trace

: scope ( -- )  \ very crude continuous ADC capture w/ graph plot
  1000 systick-hz
  TFT-BL ioc!
  tft-init
  micros clear  micros swap - .  \ us to clear the screen
  micros border micros swap - .  \ us to draw the border
  micros grid   micros swap - .  \ us to draw the grid
  TFT-BL ios!

  11 dac1-awg
  adc-buffer PB0 162 adc1-dma

  begin
\   ticks @
    \ grab and draw the trace
    301 0 do
      adc-buffer drop i 2* + h@ 20 / 1+
      dup trace i 2* + h!  \ also save a copy in a buffer
      i pp
    loop
    40 ms  \ leave the trace on the screen for a while
    \ bail out on key press, with the trace stil showing
    key? 0= while
    \ clear the trace again
    tft-bg @ tft-fg !
    301 0 do
      trace i 2* + h@
      i pp
    loop
    $FFFF tft-fg !
    grid  \ redraw the grid
\   ticks @ swap - .
  repeat ;

\ -----------------------------------------------------------------------------

( code-size ) here swap - .
cornerstone <<<lib-hmv>>>
( flash-end ) here hex.
include d
\ vim: set ft=forth :
